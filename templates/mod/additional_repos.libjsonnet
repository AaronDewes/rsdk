local soc_family = import "../lib/soc_family.libjsonnet";
local product_soc = import "../lib/product_soc.libjsonnet";
local product_soc_family(product) = soc_family(product_soc(product));

local vscodium_url(radxa_mirror) = (
    if radxa_mirror == ""
    then
        "https://download.vscodium.com/"
    else
        radxa_mirror
) + "debs";

local radxa_url(radxa_mirror) = (
    if radxa_mirror == ""
    then
        "https://radxa-repo.github.io/"
    else
        radxa_mirror
);

function(suite, radxa_mirror, radxa_repo_suffix, product, temp_dir) {
    mmdebstrap+: {
        packages+: [
            "radxa-archive-keyring",
            "vscodium-archive-keyring",
        ],
        "setup-hooks"+: [
            'echo deb %(vscodium_url)s vscodium main > "$1/etc/apt/sources.list.d/vscodium.list"'
                % {
                    vscodium_url: vscodium_url(radxa_mirror)
                },
            'echo deb %(radxa_url)s%(suite)s %(suite)s main > "$1/etc/apt/sources.list.d/radxa.list"'
                % {
                    radxa_url: radxa_url(radxa_mirror),
                    suite: suite + radxa_repo_suffix,
                },
            'echo deb %(radxa_url)s%(suite)s %(product_soc_family)s-%(suite)s main > "$1/etc/apt/sources.list.d/radxa-%(product_soc_family)s.list"'
                % {
                    radxa_url: radxa_url(radxa_mirror),
                    suite: suite + radxa_repo_suffix,
                    product_soc_family: product_soc_family(product),
                },
            'curl -L -o "$1/etc/radxa_apt_snapshot" %(radxa_url)s%(suite)s/pkgs.json'
                % {
                    radxa_url: radxa_url(radxa_mirror),
                    suite: suite + radxa_repo_suffix,
                },
        ],
        "customize-hooks"+: [
            'APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" apt-get update -oDPkg::Chroot-Directory="$1"',
            'APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" DEBIAN_FRONTEND=noninteractive NEEDRESTART_SUSPEND=1 apt-get full-upgrade -oDPkg::Chroot-Directory="$1" -y --allow-downgrades',
            'APT_CONFIG="$MMDEBSTRAP_APT_CONFIG" DEBIAN_FRONTEND=noninteractive NEEDRESTART_SUSPEND=1 apt-get autoremove -oDPkg::Chroot-Directory="$1" -y --purge',
            'sed -i "s|^deb|deb [signed-by=\"/usr/share/keyrings/vscodium-archive-keyring.gpg\"]|g" "$1"/etc/apt/sources.list.d/vscodium.list',
            'sed -i "s|^deb|deb [signed-by=\"/usr/share/keyrings/radxa-archive-keyring.gpg\"]|g" "$1"/etc/apt/sources.list.d/radxa*.list',
        ],
    }
}
