local distro_check = import "../../../lib/distro_check.libjsonnet";

function(suite, product, temp_dir, vendor_packages=false) {
    mmdebstrap+: {
        packages+:
        [
            // Core system package
            "init",
            "initramfs-tools",
            "sudo",
        ] +

        // Firmware
(if distro_check(suite) == "debian"
then
        [
            "firmware-brcm80211",
            "firmware-realtek",
        ]
else if distro_check(suite) == "ubuntu"
then
        [
            "linux-firmware",
        ]
else
        []
) +

        [
            // System utils
            "cloud-initramfs-growroot",
            "ssh",
            "systemd-timesyncd",
        ] +

        [
            // Network
            "bluetooth",
            "iw",
            "network-manager",
            "wpasupplicant",
        ] +

        [
            // Radxa
            "radxa-bootutils",
            "radxa-firmware",
            "radxa-udev",
            "rsetup",
            "rsetup-config-first-boot",
            "u-boot-%s" % [product],
            "linux-headers-%s" % [product],
            "linux-image-%s" % [product],
        ],

        "essential-hooks"+:
        [
            |||
                chroot "$1" \
                apt-get install -y %s task-%s \
                -o "Dpkg::Options::=--force-confnew"
            ||| % [(if distro_check(suite) == "debian" && vendor_packages
                    then
                        "--install-recommends"
                    else
                        "--no-install-recommends"
                    ), product],
        ],

        "customize-hooks"+:
        [
            'echo "copy_exec /usr/bin/grep /bin" >> "$1/usr/share/initramfs-tools/hooks/growroot"',
            'echo "copy_exec /usr/bin/sed /bin" >> "$1/usr/share/initramfs-tools/hooks/growroot"',
            'echo "copy_exec /usr/bin/rm /bin" >> "$1/usr/share/initramfs-tools/hooks/growroot"',
            'echo "copy_exec /usr/bin/awk /bin" >> "$1/usr/share/initramfs-tools/hooks/growroot"',
            '$BDEBSTRAP_HOOKS/enable-units "$1" haveged',
        ],
    },
}
